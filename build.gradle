plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'com.vaadin' version '24.4.8'
	id 'checkstyle'
	id 'com.github.spotbugs' version '6.0.18'
	id 'jacoco'
}

group = 'me.hoyeonj'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
	set('vaadinVersion', "24.4.8")
}

dependencies {
	// spring boot
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.vaadin:vaadin-spring-boot-starter'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation 'org.flywaydb:flyway-core'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// html documents parser
	implementation group: 'org.jsoup', name: 'jsoup', version: '1.18.1'

	// http client
	implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
	implementation group: 'com.microsoft.playwright', name: 'playwright', version: '1.47.0'

	// test
	runtimeOnly 'com.h2database:h2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
	}
}

test {
	useJUnitPlatform()
}

tasks.named('test') {
	outputs.dir snippetsDir
	finalizedBy jacocoTestReport
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

checkstyle {
	toolVersion = '10.18.2'
	configFile = file("${project.rootDir}/config/checkstyle/google-check.xml")
	ignoreFailures = false
	maxWarnings = 30
}

spotbugs {
	toolVersion = '4.8.6'
}


tasks.spotbugsMain {
	reports.create("html") {
		required = true
		outputLocation = layout.buildDirectory.file('reports/spotbugs/spotbugs.html')
		setStylesheet("fancy-hist.xsl")
	}
}

jacoco {
	toolVersion = "0.8.12"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.8
			}
		}
//		excludes = [
//		]
	}
}